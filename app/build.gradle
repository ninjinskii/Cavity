plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "com.google.devtools.ksp"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    compileSdk 35
    namespace "com.louis.app.cavity"
    testNamespace "com.louis.app.testcavity"

    defaultConfig {
        applicationId "com.louis.app.cavity"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 34
        versionName "1.9.1"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }

        sourceSets {
            named("androidTest") {
                assets.srcDir("$projectDir/schemas")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def nav_version = "2.8.9"
    def room_version = "2.7.2"
    def camerax_version = "1.4.2"
    def paging_version = "3.3.6"
    def lifecycle_version = "2.8.4"
    def work_version = "2.10.1"

    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Ktx core
    implementation "androidx.core:core-ktx:1.16.0"

    // AppCompat
    implementation "androidx.appcompat:appcompat:1.7.0"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:2.2.1"

    // Tests
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"
    implementation 'androidx.test:monitor:1.6.1'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.4.0"

    // Material
    implementation "com.google.android.material:material:1.12.0"

    // Fragments
    implementation "androidx.fragment:fragment-ktx:1.8.8"

    // Navigation component
    // Navigation component 2.9.0 is causing a crash on navigation with arguments, 100% of the times on statistics bottle picking. Wait & see
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Room
    // Starting 2.7.0, Room is causing a memory leak on HistoryViewModel when leaving HistoryFragment. Wait & see
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    ksp "androidx.room:room-compiler:$room_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.16.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.16.0"

    // CameraX
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.14"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.15.2"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Animated TextView
    implementation "com.robinhood.ticker:ticker:2.0.4"

    // Splash Screen
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:3.0.0"
    implementation "com.squareup.retrofit2:converter-moshi:3.0.0"

    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Sentry
    implementation "io.sentry:sentry-android:8.17.0"
}

tasks.register('printVersionCode') {
    doLast {
        println android.defaultConfig.versionCode
    }
}
