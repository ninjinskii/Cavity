apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdk 34
    namespace "com.louis.app.cavity"
    testNamespace "com.louis.app.testcavity"

    defaultConfig {
        applicationId "com.louis.app.cavity"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 27
        versionName "1.6.1"

        javaCompileOptions {
            annotationProcessorOptions {
                compilerArgumentProviders(
                        new RoomSchemaArgProvider(new File(projectDir, "schemas"))
                )
            }
        }

        sourceSets {
            // Adds exported schema location as test app assets.
            getByName("androidTest").assets.srcDir("$projectDir/schemas")
        }


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.test:monitor:1.6.1'
    def nav_version = "2.7.5"
    def room_version = "2.6.1"
    def camerax_version = "1.3.0"
    def paging_version = "3.2.1"
    def lifecycle_version = "2.6.2"
    def work_version = "2.9.0"

    implementation fileTree(dir: "libs", include: ["*.jar"])

    testImplementation("androidx.room:room-testing:2.6.1")


    // Ktx core
    implementation "androidx.core:core-ktx:1.12.0"

    // AppCompat
    implementation "androidx.appcompat:appcompat:1.6.1"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // Tests
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.3.2"

    // Material
    implementation "com.google.android.material:material:1.10.0"

    // Fragments
    implementation "androidx.fragment:fragment-ktx:1.6.2"

    // Navigation component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    kapt "androidx.room:room-compiler:$room_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.11.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.11.0"

    // CameraX
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.13"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.11.0"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Animated TextView
    implementation "com.robinhood.ticker:ticker:2.0.4"

    // Splash Screen
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Sentry
    implementation "io.sentry:sentry-android:7.6.0"
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        // Note: If you're using KAPT and javac, change the line below to
        // return ["-Aroom.schemaLocation=${schemaDir.path}".toString()].
        return ["room.schemaLocation=${schemaDir.path}".toString()]
    }
}
